plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def getKeystoreProperties(fileName) {
    def keystorePropertiesFile = rootProject.file(fileName)
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    return keystoreProperties
}

android {
    namespace 'cz.zachranobed.app'

    compileSdkVersion 35
    ndkVersion '29.0.13599879'

    compileOptions {
        // Version 10+ of the flutter_local_notifications plugin is now relies on desugaring to support scheduled
        // notifications with backwards compatibility on older versions of Android.
        // https://pub.dev/packages/flutter_local_notifications#gradle-setup
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "cz.zachranobed"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-build-configuration.
        minSdkVersion 23
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            def properties = getKeystoreProperties("keystore/zob_keystore.properties")
            keyAlias properties['keyAlias']
            keyPassword properties['keyPassword']
            storeFile file(properties['keystoreFile'])
            storePassword properties['keystorePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            dimension "default"
            resValue "string", "app_name", "dev flavor"
            applicationId "cz.etnetera.mobile.zachranobed.dev"
        }
        stage {
            dimension "default"
            resValue "string", "app_name", "stage flavor"
            applicationId "cz.etnetera.mobile.zachranobed.test"
        }
        prod {
            dimension "default"
            resValue "string", "app_name", "prod flavor"
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    // Version 10+ of the flutter_local_notifications plugin is now relies on desugaring to support scheduled
    // notifications with backwards compatibility on older versions of Android.
    // https://pub.dev/packages/flutter_local_notifications#gradle-setup
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}
